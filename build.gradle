buildscript {
    ext {
        mod_menu_version = '1.14.6+build.31'
        cloth_config_version = '4.8.2'
        auto_config1u_version = '3.3.1'
    }
}

plugins {
    id 'fabric-loom' version '0.5.34'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '1.1.0'
    id 'com.github.breadmoirai.github-release' version "2.2.10"
    id 'com.novoda.build-properties' version '0.4.1'
    id 'com.github.ben-manes.versions' version '0.38.0'
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "CURSEFORGE_API_KEY": "N/A",
                "MODRINTH_TOKEN"    : "N/A",
                "GITHUB_TOKEN"      : "N/A"
        ])
    }
}


sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = "${project.repoName}-${project.loader}-${project.minecraft_version}"

repositories {
    maven {
        name = 'CurseForge'
        url = 'https://minecraft.curseforge.com/api/maven'
    }
    maven {
        name = 'Cloth Config'
        url = 'https://dl.bintray.com/shedaniel/cloth-config-2'
    }
    maven {
        name = 'Auto Config (updated fork)'
        url = 'https://dl.bintray.com/shedaniel/autoconfig1u'
    }
    maven {
        name = 'prospector-fabric'
        url = 'https://maven.fabricmc.net/io/github/prospector/modmenu/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("io.github.prospector:modmenu:${mod_menu_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation("me.shedaniel.cloth:config-2:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
        exclude module: "modmenu"
    }
    include "me.shedaniel.cloth:config-2:${cloth_config_version}"

    modImplementation("me.sargunvohra.mcmods:autoconfig1u:${auto_config1u_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include "me.sargunvohra.mcmods:autoconfig1u:${auto_config1u_version}"
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand project.properties
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs = ['-Xlint:deprecation']
}

task sourcesJar(type: org.gradle.jvm.tasks.Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {
        // mavenLocal()
    }
}

curseforge {
    apiKey = buildProperties.secrets['CURSEFORGE_API_KEY'].string
    project {
        id = "${project.curseforge_project_id}"
        changelogType = 'markdown'
        addGameVersion project.minecraft_version
        addGameVersion project.loader
        changelog = getLatestChangeLog()
        releaseType = "${project.curseforge_release_type}"
        mainArtifact(remapJar) {
            displayName = "${archivesBaseName}-${project.version}"
        }
        afterEvaluate {
            uploadTask.dependsOn('remapJar')
        }
        relations {
            requiredDependency 'fabric-api'
            embeddedLibrary 'auto-config-updated-api'
            embeddedLibrary 'cloth-config'
            optionalDependency 'modmenu'
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

import com.modrinth.minotaur.TaskModrinthUpload
task publishModrinth(type: TaskModrinthUpload) {
    token = buildProperties.secrets['MODRINTH_TOKEN'].string
    projectId = "${project.modrinth_project_id}"
    versionNumber = "${project.minecraft_version}-${project.version}-${project.loader}"
    versionName = "${archivesBaseName}-${project.version}"
    uploadFile = remapJar
    addGameVersion(project.minecraft_version)
    addLoader(project.loader.toLowerCase())
    releaseType = "${project.curseforge_release_type}"
    changelog = getLatestChangeLog()

    afterEvaluate {
        tasks.publishModrinth.dependsOn(remapJar)
    }
}

githubRelease {
    owner = "RakSrinaNa"
    repo = project.repoName
    token = buildProperties.secrets['GITHUB_TOKEN'].string
    tagName = "${archivesBaseName}-${project.version}"
    releaseName = "${archivesBaseName}-${project.version}"
    targetCommitish = "${project.minecraft_version}-fabric"
    body {
        return getLatestChangeLog()
    }
}

import java.time.LocalDate
import java.time.format.DateTimeFormatter

String getAllChangeLog() {
    final old_log = file("CHANGELOG.md")
    final header_file = file("header.md")

    def all_log = old_log.exists() ? old_log.text : "No change log provided."
    def header = header_file.exists() ? header_file.text : ""
    return header + System.lineSeparator() + all_log
}

String getLatestChangeLog() {
    final def log_file = file("changes.md")
    final def header_file = file("header.md")
    def log = log_file.exists() ? log_file.text : "No change log provided."
    def header = header_file.exists() ? header_file.text : ""
    return header + System.lineSeparator() + log
}

task makeChangeLog() {
    doLast {
        moveOldLog()
    }
}

void moveOldLog() {
    def new_log_file = file("changes.md")
    def old_log_file = file("CHANGELOG.md")

    if (new_log_file.exists()) {
        if (!old_log_file.exists())
            old_log_file.createNewFile()
        def dateFormat = DateTimeFormatter.ofPattern("yyyy-MM-dd")
        def currDate = LocalDate.now()
        def combined = "## [${project.loader}-${project.minecraft_version}-${version}] - " + currDate.format(dateFormat) + System.lineSeparator() + new_log_file.text.trim() + System.lineSeparator() + System.lineSeparator() + old_log_file.text
        old_log_file.setText(combined)
    }
}
