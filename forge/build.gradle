plugins {
    alias(libs.plugins.forge)
}

minecraft {
    mappings channel: "official", version: "${libs.versions.minecraftVersion.get()}"
    copyIdeResources = true
    reobf = false

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "error"
        }

        client {
            taskName "runFTForgeClient"
            workingDirectory project.file("./run/client")
        }

        server {
            taskName "runFTForgeServer"
            workingDirectory project.file("./run/server")
            args "--nogui"
        }

        gameTestServer {
            taskName "runFTForgeTestServer"
            workingDirectory project.file("./run/test")
            property "forge.enabledGameTestNamespaces", "fallingtree"
        }

        data {
            taskName "runFTForgeData"
            workingDirectory project.file("./run/data")
            args '--mod', 'fallingtree', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }
}

configurations {
    library
    implementation.extendsFrom library
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft(libs.forge)
    library(project(":common"))

    implementation("me.shedaniel.cloth:cloth-config-forge:${libs.versions.clothConfigVersion.get()}")

    // Hack fix for now, force jopt-simple to be exactly 5.0.4 because Mojang ships that version, but some transitive dependencies request 6.0+
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } }
}

ext {
    minecraftVersion = libs.versions.minecraftVersion.get()
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from "src/main/resources", "../common/src/main/resources"

    filesMatching(["META-INF/mods.toml", "pack.mcmeta"]) {
        expand project.properties
    }
}

// Merge the resources and classes into the same directory.
// This is done because java expects modules to be in a single directory.
// And if we have it in multiple we have to do performance intensive hacks like having the UnionFileSystem
// This will eventually be migrated to ForgeGradle so modders don't need to manually do it. But that is later.
sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}